import org.softlang.qegal.buildins.decompose.*
import org.softlang.qegal.buildins.*
import org.softlang.qegal.buildins.string.*
import org.apache.jena.reasoner.rulesys.builtins.*

/* Default softlang namespace */
@prefix sl: <http://org.softlang.com/>.

// Identification of Factory and Package.
(?epackage, sl:elementOf, sl:Java) 
	(?epackage, sl:refersToExtendedTypes, "EPackage") -> 
		(?epackage, sl:elementOf, sl:EcorePackageJava).

(?efactory, sl:elementOf, sl:Java) 
	(?efactory, sl:refersToExtendedTypes, "EFactory") -> 
		(?efactory, sl:elementOf, sl:EcoreFactoryJava).
		
// Extraction of nsuri.
(?epackage, sl:elementOf, sl:EcorePackageJava) 
	UriJava(?epackage, "type[1]/members/variables[name/identifier='eNS_URI']/initializer/value/value", ?nsUri) ->
		(?epackage, sl:nsURI, ?nsUri).

(?efactory, sl:elementOf, sl:EcoreFactoryJava) 
	UriJava(?epackage, "type[1]/members/variables[name/identifier='eNS_URI']/initializer/value/value", ?nsUri) ->
		(?epackage, sl:nsURI, ?nsUri).

// Interfaces are detected by subclassing.
(?eclassInterface, sl:elementOf, sl:Java)
	(?eclassInterface, sl:refersToExtendedTypes, "EObject") ->
		(?eclassInterface, sl:elementOf, sl:EcoreModelInterfaceJava).
		
(?super, sl:elementOf, sl:EcoreModelInterfaceJava)
	(?eclass, sl:refersToExtendedTypes, ?super) ->
		(?eclass, sl:elementOf, sl:EcoreModelInterfaceJava).		

// Implementations are detected by subclassing interfaces.
(?eclassImpl, sl:elementOf, sl:Java)
	(?eclassImpl, sl:refersToImplementedTypes, ?eclassInterface)
	(?eclassInterface, sl:elementOf, sl:EcoreModelInterfaceJava)->
			(?eclassImpl, sl:elementOf, sl:EcoreModelImplJava).
				
(?eAttributeInterfaceSignature, sl:elementOf, sl:MemberJava)
	(?eclassInterface, sl:elementOf, sl:EcoreModelInterfaceJava)
	(?eAttributeInterfaceSignature, sl:partOf, ?eClassInterface)
	StrJava(?eAttributeInterfaceSignature, "/comment/value/content", ?comment) 
	Regex(?comment, "(?s).*[\\n\\r].*@generated.*") -> 
		(?eAttributeInterfaceSignature, sl:elementOf, sl:InterfaceMethodSignatureJava).

 (?eAttributeImplementation, sl:elementOf, sl:AttributeJava)
 	(?eclassImpl, sl:elementOf, sl:EcoreModelImplJava)
	(?eAttributeImplementation, sl:partOf, ?eclassImpl)
	StrJava(?eAttributeImplementation, "/comment/value/content", ?comment) 
	Regex(?comment, "(?s).*[\\n\\r].*@generated.*") -> 
		(?eAttributeImplementation, sl:elementOf, sl:ClassAttributeJava).
