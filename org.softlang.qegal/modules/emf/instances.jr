@prefix sl: <http://org.softlang.com/>.
@prefix system: <http://org.softlang.com/qegal/>.
@prefix omg: <http://www.omg.org/>.

// TODO: Maybe this is more and uri package.
// (i) scheme, (ii) schemespecific part, (iii) optional fragment
// Extract namespaces. (TODO: Can be extracted to xml module)
(?file sl:elementOf sl:XML) -> uriXml(?file, ?file, sl:xmlns "/namespace::*").

// XMI files detection with element limit.
(?file sl:elementOf sl:XML) (?file sl:xmlns omg:XMI) ->
    (?file sl:elementOf sl:XMI).

// Decompose Ecore into structural containment features needed for instance decomposition.
(?classifier sl:elementOf sl:EcoreClassifierXMI) ->
    decXml(?classifier,"/eStructuralFeatures[@containment='true']",sl:partOf,?classifier)
    decXml(?classifier,"/eStructuralFeatures[@containment='true']",sl:elementOf,sl:EcoreStructuralFeatureXMI).

// Extract reference and names of these structural features.
(?structuralFeature sl:elementOf,sl:EcoreStructuralFeatureXMI) (?structuralFeature sl:partOf ?classifier)
    (?classifier sl:nsUri ?nsUri) strXml(?structuralFeature,"/@eType", ?type) uriConcat(?nsUri,?type,?references) ->
    (?structuralFeature sl:references ?references).

(?structuralFeature sl:elementOf,sl:EcoreStructuralFeatureXMI) ->
    strXml(?structuralFeature,?structuralFeature,sl:name, "/@name").

// Root conformance.
(?file sl:elementOf sl:XMI) (?file sl:xmlns ?nsUri) (?xmiPackage sl:nsUri ?nsUri)
    (?xmiPackage sl:elementOf sl:EcorePackageXMI) (?xmiPackage sl:nsPrefix ?nsPrefix)
    (?xmiClassifier sl:partOf ?xmiPackage) (?xmiClassifier sl:elementOf sl:EcoreClassifierXMI)
    (?xmiClassifier sl:name ?classifierName) strConcat("/",?nsPrefix,":",?classifierName,?xpath) ->
    decXml(?file,?xpath,sl:conformsTo,?xmiClassifier)
    decXml(?file,?xpath,sl:elementOf,sl:XMIFragment).

// Nested conformance.
(?xmiFragment sl:elementOf sl:XMIFragment)
    (?xmiFragment sl:conformsTo ?xmiClassifier)
    (?xmiStructuralFeature sl:partOf ?xmiClassifier)
    (?xmiStructuralFeature sl:elementOf sl:EcoreStructuralFeatureXMI)
    (?xmiStructuralFeature sl:name ?sfname)
    (?xmiStructuralFeature sl:references ?xmiClassifierUri)
    (?referencesXmiClassifier sl:uri ?xmiClassifierUri)
    (?referencesXmiClassifier sl:elementOf sl:EcoreClassifierXMI)
    strConcat("/",?sfname,?xpath) ->
    decXml(?xmiFragment,?xpath,sl:conformsTo,?referencesXmiClassifier)
    decXml(?xmiFragment,?xpath,sl:elementOf,sl:XMIFragment).
